version = '2.2.3'

project.ext.set("artifactName", "oracle-ddl2svn-v" + project.version + ".zip")

repositories {
    maven {
        url "http://scheme2ddl.googlecode.com/svn/m2/releases"
    }
}

configurations {
    distributive
}

dependencies {
    distributive('com.googlecode:scheme2ddl:2.2.3') {
        transitive = false
    }
}

task zip(type: Zip) {
    archiveName = project.ext.artifactName
    from 'src/dist'
    from({ zipTree(configurations.distributive.singleFile) }) {
        include 'scheme2ddl.config.xml'
        rename 'scheme2ddl.config.xml', 'scheme2ddl.config.xml.optional.sample'
    }
    from configurations.distributive
    rename 'scheme2ddl.*.jar', 'scheme2ddl.jar'
}

task release(dependsOn: 'zip') << {
    ant.taskdef(name: 'googlecode_upload', classname : 'net.bluecow.googlecode.ant.GoogleCodeUploadTask') {
        classpath {
            fileset(dir: 'lib', includes: 'ant-googlecode-0.0.3.jar')
        }
    }
    ant.property(file: 'credentials.properties')
    ant.googlecode_upload(
            username: ant.properties.googlecode_username,
            password: ant.properties.googlecode_password,
            projectname: 'oracle-ddl2svn',
            filename: project.ext.artifactName,
            targetfilename: project.ext.artifactName,
            summary: "release " + project.version,
            labels: "Featured, Type-Archive"
    )
}

